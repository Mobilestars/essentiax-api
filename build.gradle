plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'de.scholle.essentiax'
version = '1.0.0'
description = 'EssentiaX Public API'

java {
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'de.scholle.essentiax'
            artifactId = 'api'
            version = project.version

            pom {
                name.set('EssentiaX API')
                description.set('Öffentliche API für Addon-Entwickler von EssentiaX')
                url.set('https://github.com/Mobilestars/essentiax-api')
                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/licenses/MIT')
                    }
                }
                developers {
                    developer {
                        id.set('scholle')
                        name.set('Scholle')
                    }
                }
                scm {
                    connection.set('scm:git:git://github.com/Mobilestars/essentiax-api.git')
                    developerConnection.set('scm:git:ssh://git@github.com:Mobilestars/essentiax-api.git')
                    url.set('https://github.com/Mobilestars/essentiax-api')
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = project.findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = project.findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
